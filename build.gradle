plugins {
    id 'com.diffplug.spotless' version "${spotlessVersion}" apply false
    id 'com.github.spotbugs' version "${spotbugsVersion}" apply false
}

allprojects {
    apply plugin: 'java-library'
    repositories {
        mavenLocal()
        mavenCentral()
    }
    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }
    compileTestJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }
    test {
        useJUnitPlatform()
    }
    // spotless
    apply plugin: 'com.diffplug.spotless'
    spotless {
        encoding 'UTF-8'
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            custom('Refuse wildcard imports', {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }
    // spotbugs
    apply plugin: 'com.github.spotbugs'
    spotbugs {
        spotbugsTest.enabled = false
        // https://spotbugs.readthedocs.io/en/stable/detectors.html
        omitVisitors.addAll('FindReturnRef', 'DoInsideDoPrivileged')
    }
}

tasks.register('installGitHook', Copy) {
    from "$rootProject.rootDir/.githooks"
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}
compileJava.dependsOn installGitHook
